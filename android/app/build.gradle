def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 29
    // Cf. https://github.com/flutter/flutter/issues/53657#issuecomment-642236438
    ndkVersion '21.3.6528147'

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "io.github.x_wei.flutter_catalog"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Add this line to resolve "unable to merge dex" error, c.f. 
        // https://github.com/flutter/flutter/issues/14874.
        multiDexEnabled true
        ndk.abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
    }
    dexOptions {
        preDexLibraries = false
    }

    signingConfigs {
    release {
      def keystoreProperties = new Properties()
      def keystorePropertiesFile = rootProject.file('key.properties')
      if (keystorePropertiesFile.exists()) {
          keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
          keyAlias keystoreProperties['keyAlias']
          keyPassword keystoreProperties['keyPassword']
          storeFile file(keystoreProperties['storeFile'])
          storePassword keystoreProperties['storePassword']
      }
    }
    }
    buildTypes {
        release {
            // Add multidex-config.pro and this line to void multidex problem
            // for API version <21. 
            // Cf. https://developer.android.com/studio/build/multidex#keep.
            multiDexKeepProguard file('multidex-config.pro')
            signingConfig signingConfigs.release
            // Enable symbolicated native crash stack traces, cf. 
            // https://developer.android.com/studio/build/shrink-code#android_gradle_plugin_version_41_or_later
            ndk {
              debugSymbolLevel 'FULL'
            }
            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    // Cf. https://firebase.google.com/docs/android/setup#add-sdks.
    implementation 'com.google.firebase:firebase-analytics:17.5.0'
    implementation 'com.google.firebase:firebase-auth:19.4.0'
    implementation 'com.google.firebase:firebase-firestore:21.6.0'
    // Cf. https://firebase.google.com/docs/ml-kit/android/read-barcodes.
    implementation 'com.google.firebase:firebase-ml-vision:24.0.3'
    implementation 'com.google.firebase:firebase-ml-vision-barcode-model:16.0.1'
    implementation 'com.google.firebase:firebase-ml-vision-image-label-model:20.0.1'
    implementation 'com.google.firebase:firebase-ml-vision-face-model:20.0.1'
}

apply plugin: 'com.google.gms.google-services'